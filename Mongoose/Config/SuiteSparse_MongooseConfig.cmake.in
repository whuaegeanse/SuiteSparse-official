#-------------------------------------------------------------------------------
# SuiteSparse/Mongoose/cmake_modules/SuiteSparse_MongooseConfig.cmake
#-------------------------------------------------------------------------------

# The following copyright and license applies to just this file only, not to
# the library itself:
# MongooseConfig.cmake, Copyright (c) 2023, Timothy A. Davis.  All Rights Reserved.
# SPDX-License-Identifier: BSD-3-clause

#-------------------------------------------------------------------------------

# Finds the SuiteSparse_Mongoose include file and compiled library.
# The following targets are defined:
#   SuiteSparse::Mongoose           - for the shared library (if available)
#   SuiteSparse::Mongoose_static    - for the static library (if available)

# For backward compatibility the following variables are set:

# SUITESPARSE_MONGOOSE_INCLUDE_DIR - where to find Mongoose.hpp
# SUITESPARSE_MONGOOSE_LIBRARY     - dynamic Mongoose library
# SUITESPARSE_MONGOOSE_STATIC      - static Mongoose library
# SUITESPARSE_MONGOOSE_LIBRARIES   - libraries when using Mongoose
# SuiteSparse_Mongoose_FOUND       - true if Mongoose found

# Set ``CMAKE_MODULE_PATH`` to the parent folder where this module file is
# installed.

#-------------------------------------------------------------------------------

@PACKAGE_INIT@

set ( MONGOOSE_DATE "@Mongoose_DATE@" )
set ( MONGOOSE_VERSION_MAJOR @Mongoose_VERSION_MAJOR@ )
set ( MONGOOSE_VERSION_MINOR @Mongoose_VERSION_MINOR@ )
set ( MONGOOSE_VERSION_PATCH @Mongoose_VERSION_PATCH@ )
set ( SUITESPARSE_MONGOOSE_VERSION "@Mongoose_VERSION_MAJOR@.@Mongoose_VERSION_MINOR@.@Mongoose_VERSION_PATCH@" )

# Check for dependent targets
include ( CMakeFindDependencyMacro )

# Look for SuiteSparse_config target
if ( @SUITESPARSE_IN_BUILD_TREE@ )
    if ( NOT TARGET SuiteSparse::SuiteSparseConfig )
        # First check in a common build tree
        find_dependency ( SuiteSparse_config @SUITESPARSE_CONFIG_VERSION_MAJOR@.@SUITESPARSE_CONFIG_VERSION_MINOR@
            PATHS ${CMAKE_SOURCE_DIR}/../SuiteSparse_config/build NO_DEFAULT_PATH )
        # Then, check in the currently active CMAKE_MODULE_PATH
        if ( NOT SuiteSparse_config_FOUND )
            find_dependency ( SuiteSparse_config @SUITESPARSE_CONFIG_VERSION_MAJOR@.@SUITESPARSE_CONFIG_VERSION_MINOR@ )
        endif ( )
    endif ( )
else ( )
    if ( NOT TARGET SuiteSparse::SuiteSparseConfig )
        find_dependency ( SuiteSparse_config @SUITESPARSE_CONFIG_VERSION_MAJOR@.@SUITESPARSE_CONFIG_VERSION_MINOR@ )
    endif ( )
endif ( )
if ( NOT SuiteSparse_config_FOUND )
    set ( SuiteSparse_Mongoose_FOUND OFF )
    return ( )
endif ( )


# Import target
include ( ${CMAKE_CURRENT_LIST_DIR}/SuiteSparse_MongooseTargets.cmake )

# The following is only for backward compatibility with FindMongoose.

set ( _target_shared SuiteSparse::Mongoose )
set ( _target_static SuiteSparse::Mongoose_static )
set ( _var_prefix "SUITESPARSE_MONGOOSE" )

if ( NOT @BUILD_SHARED_LIBS@ AND NOT TARGET ${_target_shared} )
    # make sure there is always an import target without suffix )
    add_library ( ${_target_shared} ALIAS ${_target_static} )
endif ( )

get_target_property ( ${_var_prefix}_INCLUDE_DIR ${_target_shared} INTERFACE_INCLUDE_DIRECTORIES )
if ( ${_var_prefix}_INCLUDE_DIR )
    # First item in SuiteSparse targets contains the "main" header directory.
    list ( GET ${_var_prefix}_INCLUDE_DIR 0 ${_var_prefix}_INCLUDE_DIR )
endif ( )
get_target_property ( ${_var_prefix}_LIBRARY ${_target_shared} IMPORTED_IMPLIB )
if ( NOT ${_var_prefix}_LIBRARY )
    get_target_property ( _library_chk ${_target_shared} IMPORTED_LOCATION )
    if ( EXISTS ${_library_chk} )
        set ( ${_var_prefix}_LIBRARY ${_library_chk} )
    endif ( )
endif ( )
if ( TARGET ${_target_static} )
    get_target_property ( ${_var_prefix}_STATIC ${_target_static} IMPORTED_LOCATION )
endif ( )

# Check for most common build types
set ( _config_types "Debug" "Release" "RelWithDebInfo" "MinSizeRel" "None" )

get_property ( _isMultiConfig GLOBAL PROPERTY GENERATOR_IS_MULTI_CONFIG )
if ( _isMultiConfig )
    # For multi-configuration generators (e.g., Visual Studio), prefer those
    # configurations.
    list ( PREPEND _config_types ${CMAKE_CONFIGURATION_TYPES} )
else ( )
    # For single-configuration generators, prefer the current configuration.
    list ( PREPEND _config_types ${CMAKE_BUILD_TYPE} )
endif ( )

list ( REMOVE_DUPLICATES _config_types )

foreach ( _config ${_config_types} )
    string ( TOUPPER ${_config} _uc_config )
    if ( NOT ${_var_prefix}_LIBRARY )
        get_target_property ( _library_chk ${_target_shared}
            IMPORTED_IMPLIB_${_uc_config} )
        if ( EXISTS ${_library_chk} )
            set ( ${_var_prefix}_LIBRARY ${_library_chk} )
        endif ( )
    endif ( )
    if ( NOT ${_var_prefix}_LIBRARY )
        get_target_property ( _library_chk ${_target_shared}
            IMPORTED_LOCATION_${_uc_config} )
        if ( EXISTS ${_library_chk} )
            set ( ${_var_prefix}_LIBRARY ${_library_chk} )
        endif ( )
    endif ( )
    if ( TARGET ${_target_static} AND NOT ${_var_prefix}_STATIC )
        get_target_property ( _library_chk ${_target_static}
            IMPORTED_LOCATION_${_uc_config} )
        if ( EXISTS ${_library_chk} )
            set ( ${_var_prefix}_STATIC ${_library_chk} )
        endif ( )
    endif ( )
endforeach ( )

set ( SUITESPARSE_MONGOOSE_LIBRARIES ${SUITESPARSE_MONGOOSE_LIBRARY} )

macro ( suitesparse_check_exist _var _files )
  # ignore generator expressions
  string ( GENEX_STRIP "${_files}" _files2 )

  foreach ( _file ${_files2} )
    if ( NOT EXISTS "${_file}" )
      message ( FATAL_ERROR "File or directory ${_file} referenced by variable ${_var} does not exist!" )
    endif ( )
  endforeach ()
endmacro ( )

suitesparse_check_exist ( SUITESPARSE_MONGOOSE_INCLUDE_DIR ${SUITESPARSE_MONGOOSE_INCLUDE_DIR} )
suitesparse_check_exist ( SUITESPARSE_MONGOOSE_LIBRARY ${SUITESPARSE_MONGOOSE_LIBRARY} )

message ( STATUS "SuiteSparse_Mongoose version: ${SUITESPARSE_MONGOOSE_VERSION}" )
message ( STATUS "SuiteSparse_Mongoose include: ${SUITESPARSE_MONGOOSE_INCLUDE_DIR}" )
message ( STATUS "SuiteSparse_Mongoose library: ${SUITESPARSE_MONGOOSE_LIBRARY}" )
message ( STATUS "SuiteSparse_Mongoose static:  ${SUITESPARSE_MONGOOSE_STATIC}" )
